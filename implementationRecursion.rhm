#lang shplait

macro 'test_returns $fn:
         ($arg, ...): $ret
         ...':
            'block:
               check: $fn($arg, ...)
                      ~is $ret
               ...'

macro 'test_errors $fn:
         ($args, ...): $value
         ...':
      'begin:
        check: $fn($args, ...)
               ~raises $value
        ...'

type Value
| intV(n :: Int)
| closV(arg :: Symbol,
        body :: Exp,
        env :: Env)

type Exp
| intE(n :: Int)
| idE(s :: Symbol)
| plusE(l :: Exp,
        r :: Exp)
| multE(l :: Exp,
        r :: Exp)
| letrecE(n :: Symbol,
          rhs :: Exp,
          body :: Exp)
| funE(n :: Symbol,
       body :: Exp)
| appE(fn :: Exp,
       arg :: Exp)
| if0E(condE :: Exp,
       then :: Exp,
       else :: Exp)

type Binding
| bind(name :: Symbol,
       val :: Boxof(Optionof(Value)))


type Env = Listof(Binding)

def mt_env = []
def extend_env = cons

// parse ----------------------------------------
fun parse(s :: Syntax) :: Exp:
  cond
  | syntax_is_integer(s):
      intE(syntax_to_integer(s))
  | syntax_is_symbol(s):
      idE(syntax_to_symbol(s))
  | ~else:
      match s
      | 'letrec $n = $e1: $e2':
          letrecE(syntax_to_symbol(n),
                  parse(e1),
                  parse(e2))
      | 'if $cond_e == 0 | $e1 | $e2':
          if0E(parse(cond_e),
               parse(e1),
               parse(e2))
      | '$left + $right':
          plusE(parse(left),
                parse(right))
      | '$left * $right':
          multE(parse(left),
                parse(right))
      | 'fun ($id): $body':
          funE(syntax_to_symbol(id),
               parse(body))
      | '$fn($arg)':
          appE(parse(fn),
               parse(arg))
      | '($e)':
          parse(e)
      | ~else:
          error(#'parse, "invalid input: " +& s)

module test:
  check: interp(parse('letrec fac = (fun(x):
                                       if x == 0
                                       | 1
                                       | x * fac(x + -1)):
                         fac(5)'),
                mt_env)
         ~is intV(120)

  test_returns parse:
    ('2'): intE(2)
    ('x'): idE(#'x)
    ('2 + 1'): plusE(intE(2), intE(1))
    ('3 * 4'): multE(intE(3), intE(4))
    ('3 * 4 + 8'): plusE(multE(intE(3), intE(4)), intE(8))
    ('fun (x): 9'): funE(#'x, intE(9))
    ('double(9)'): appE(idE(#'double), intE(9))
    ('1 + double(9)'): plusE(intE(1), appE(idE(#'double), intE(9)))
    ('3 * (4 + 8)'): multE(intE(3), plusE(intE(4), intE(8)))
  check: parse('1 2')
         ~raises "invalid input"
      
// interp ----------------------------------------
fun interp(a :: Exp, env :: Env) :: Value:
  match a
  | intE(n): intV(n)
  | idE(s): lookup(s, env)
  | plusE(l, r): num_plus(interp(l, env), interp(r, env))
  | multE(l, r): num_mult(interp(l, env), interp(r, env))
  | funE(n, body): closV(n, body, env)
  | appE(fn, arg):
      match interp(fn, env)
      | closV(n, body, c_env):
          interp(body,
                 let val = interp(arg, env):
                   extend_env(bind(n, box(some(val))),
                              c_env))
      | ~else: error(#'interp, "not a function")
  | letrecE(name, rhs, body):
      let b = box(none()):
        let new_env = extend_env(bind(name, b),
                                 env):
          set_box(b, some(interp(rhs, new_env)))
          interp(body, new_env)
  | if0E(tst, thn, els):
      match interp(tst, env)
      | intV(n): if n == 0
                 | interp(thn, env)
                 | interp(els, env)
      | ~else:
          error(#'interp, "not a number")
      

module test:
  check: interp(if0E(parse('fun (x): x + x'), intE(1), intE(1)), [])
         ~raises "not a number"
  test_returns interp:
    (parse('2'), mt_env): intV(2)
    (parse('x'), extend_env(bind(#'x, box(some(intV(9)))), mt_env)): intV(9)
    (parse('2 + 1'), mt_env): intV(3)
    (parse('2 * 1'), mt_env): intV(2)
    (parse('(2 * 3) + (5 + 8)'), mt_env): intV(19)
    (parse('fun (x): x + x'), mt_env): closV(#'x, plusE(idE(#'x), idE(#'x)), mt_env)
    (parse('(fun (x): x + x)(8)'), mt_env): intV(16)
  
  test_errors interp:
    (parse('x'), mt_env): "free variable"
    (parse('1(2)'), mt_env): "not a function"
    (parse('1 + (fun (x): x)'), mt_env): "not a number"
               
// num_plus and num_mult ----------------------------------------
fun num_op(op :: (Int, Int) -> Int, l :: Value, r :: Value) :: Value:
  cond
  | l is_a intV && r is_a intV:
      intV(op(intV.n(l), intV.n(r)))
  | ~else:
      error(#'interp, "not a number")
fun num_plus(l :: Value, r :: Value) :: Value:
  num_op(fun (a, b): a+b, l, r)
fun num_mult(l :: Value, r :: Value) :: Value:
  num_op(fun (a, b): a*b, l, r)

module test:
  check: num_plus(intV(1), intV(2))
         ~is intV(3)
  check: num_mult(intV(3), intV(2))
         ~is intV(6)
  
// lookup ----------------------------------------
fun lookup(n :: Symbol, env :: Env) :: Value:
  match env
  | []: error(#'lookup, "free variable")
  | cons(b, rst_env):
      cond
      | n == bind.name(b):
          match unbox(bind.val(b))
          | none(): error(#'lookup, "use before initialization")
          | some(v): v
      | ~else:
          lookup(n, rst_env)

module test:
  check: lookup(#'x, [bind(#'x, box(none()))])
         ~raises "use before initialization"
  check: lookup(#'x, mt_env)
         ~raises "free variable"
  test_returns lookup:
    (#'x, extend_env(bind(#'x, box(some(intV(8)))), mt_env)): intV(8)
    (#'x, extend_env(bind(#'x, box(some(intV(9)))),
                     extend_env(bind(#'x, box(some(intV(8)))),
                                mt_env))): intV(9)
    (#'y, extend_env(bind(#'x, box(some(intV(9)))),
                     extend_env(bind(#'y, box(some(intV(8)))),
                                mt_env))): intV(8)