#lang shplait

type Location = Int

type Value
| intV(n :: Int)
| closV(arg :: Symbol,
        body :: Exp,
        env :: Env)
| boxV(l :: Location)


type Exp
| intE(n :: Int)
| idE(s :: Symbol)
| plusE(l :: Exp,
        r :: Exp)
| multE(l :: Exp,
        r :: Exp)
| letE(n :: Symbol,
       rhs :: Exp,
       body :: Exp)
| funE(n :: Symbol,
       body :: Exp)
| appE(fn :: Exp,
       arg :: Exp)
| boxE(arg :: Exp)
| unboxE(arg :: Exp)
| setboxE(bx :: Exp,
          val :: Exp)
| beginE(l :: Exp,
         r :: Exp)
  
type Binding
| bind(name :: Symbol,
       val :: Value)

type Env = Listof(Binding)
def extend_env = cons

type Storage
| cell(location :: Location,
       val :: Value)

type Store = Listof(Storage)
def mt_store = []
def override_store = cons

type Result
| res(v :: Value,
      s :: Store)

// parse ----------------------------------------
fun parse(s :: Syntax) :: Exp:
  cond
  | syntax_is_integer(s):
      intE(syntax_to_integer(s))
  | syntax_is_symbol(s):
      idE(syntax_to_symbol(s))
  | ~else:
      match s
      | 'let $name = $rhs:
           $body':
          letE(syntax_to_symbol(name),
               parse(rhs),
               parse(body))
      | '$left + $right':
          plusE(parse(left),
                parse(right))
      | '$left * $right':
          multE(parse(left),
                parse(right))
      | 'fun ($id): $body':
          funE(syntax_to_symbol(id),
               parse(body))
      | 'box($val_arg)':
          boxE(parse(val_arg))
      | 'unbox($bx_arg)':
          unboxE(parse(bx_arg))
      | 'set_box($bx_arg, $val_arg)':
           setboxE(parse(bx_arg), parse(val_arg))
      | '$fn($arg)':
          appE(parse(fn),
               parse(arg))
      | 'begin:
           $effect_exp
           $result_exp':
          beginE(parse(effect_exp), parse(result_exp))
      | '($e)':
          parse(e)
      | ~else:
          error(#'parse, "invalid input: " +& s)

// reslet form ------------------------------
macro 'reslet ($v_id, $sto_id) = $call:
         $body':
  'match $call
   | res($v_id, $sto_id):
       $body'

// interp ----------------------------------------
fun interp(a :: Exp, env :: Env, sto :: Store) :: Result:
  match a
  | intE(n): res(intV(n), sto)
  | idE(s): res(lookup(s, env), sto)
  | plusE(l, r): reslet (v_l, sto_l) = interp(l, env, sto):
                   reslet (v_r, sto_r) = interp(r, env, sto_l):
                     res(num_plus(v_l, v_r), sto_r)
  | multE(l, r): reslet (v_l, sto_l) = interp(l, env, sto):
                   reslet (v_r, sto_r) = interp(r, env, sto_l):
                     res(num_mult(v_l, v_r), sto_r)
  | letE(n, rhs, body):
      reslet (v_rhs, sto_rhs) = interp(rhs, env, sto):
        interp(body,
               extend_env(bind(n, v_rhs),
                          env),
               sto_rhs)
  | funE(n, body): res(closV(n, body, env), sto)
  | appE(fn, arg):
      reslet (v_fn, sto_fn) = interp(fn, env, sto):
        match v_fn
        | closV(n, body, c_env):
           reslet (v_arg, sto_arg) = interp(arg, env, sto_fn):
             reslet (newVal, newStore) = interp(body,
                                                extend_env(
                                                  bind(
                                                    n, v_arg
                                                  ),
                                                  c_env),
                                                sto_arg):
               res(newVal, newStore)
        | ~else: error(#'interp, "not a function")
  | boxE(a):
      match interp(a, env, sto)
      | res(v, sto_v):
          block:
            def l = new_loc(sto_v)
            res(boxV(l),
                override_store(cell(l, v),
                               sto_v))
  | unboxE(a):
      match interp(a, env, sto)
      | res(v, sto_v):
          match v
          | boxV(l): res(fetch(l, sto_v),
                         sto_v)
          | ~else: error(#'interp,
                         "not a box")
  | setboxE(bx, val):
      match interp(bx, env, sto)
      | res(v_b, sto_b):
          match interp(val, env, sto_b)
          | res(v_v, sto_v):
              match v_b
              | boxV(l):
                  res(v_v,
                      override_store(cell(l, v_v),
                                     sto_v))
              | ~else: error(#'interp,
                             "not a box")
  | beginE(l, r):
      match interp(l, env, sto)
      | res(v_l, sto_l):
          interp(r, env, sto_l)

fun fetch(loc :: Location, sto :: Store) :: Value:
  match sto
  | cons(c, rst_sto): if cell.location(c) == loc
                      | cell.val(c)
                      | fetch(loc, rst_sto)
  | ~else: error(#'fetch, "location not found in store")
           
fun new_loc(sto :: Store) :: Location:
  1 + max_address(sto)

fun max_address(sto :: Store) :: Location:
  match sto
  | cons(c, rst_sto): max(cell.location(c),
                          max_address(rst_sto))
  | ~else: 0
// num_plus and num_mult ----------------------------------------
fun num_op(op :: (Int, Int) -> Int, l :: Value, r :: Value) :: Value:
  cond
  | l is_a intV && r is_a intV:
      intV(op(intV.n(l), intV.n(r)))
  | ~else:
      error(#'interp, "not a number")
fun num_plus(l :: Value, r :: Value) :: Value:
  num_op(fun (a, b): a+b, l, r)
fun num_mult(l :: Value, r :: Value) :: Value:
  num_op(fun (a, b): a*b, l, r)

module test:
  check: num_plus(intV(1), intV(2))
         ~is intV(3)
  check: num_mult(intV(3), intV(2))
         ~is intV(6)
  
// lookup ----------------------------------------
fun lookup(n :: Symbol, env :: Env) :: Value:
  match env
  | []: error(#'lookup, "free variable")
  | cons(b, rst_env):
      cond
      | n == bind.name(b):
          bind.val(b)
      | ~else:
          lookup(n, rst_env)

module test:
  def store = []
  def mt_env = []
  fun run(s :: Syntax):
    def result = interp(parse(s), mt_env, store)
    def val = res.v(result)
    match val
    | intV(n): n
    | closV(arg, body, env): error(#'test, "run cannot be used with closV")
    | boxV(l): l

  check: interp(unboxE(intE(0)), [], [])
         ~raises "not a box"
  check: interp(setboxE(intE(0), intE(0)), [], [])
         ~raises "not a box"
  check: interp(parse('set_box(box(5), 6)'),
                mt_env,
                store)
         ~is res(intV(6),
                 override_store(cell(1, intV(6)),
                                override_store(cell(1, intV(5)),
                                               mt_store)))
  check: run('box(5)')
         ~is 1
  check: parse('2')
         ~is intE(2)
  check: parse('x')
         ~is idE(#'x)
  check: parse('2 + 1')
         ~is plusE(intE(2), intE (1))
  check: parse('3 * 4')
         ~is multE(intE(3), intE(4))
  check: parse('3 * 4 + 8')
         ~is plusE(multE(intE(3), intE(4)),
                   intE(8))
  check: parse('fun (x): 9')
         ~is funE(#'x, intE(9))
  check: parse('double(9)')
         ~is appE(idE(#'double), intE(9))
  check: parse('1 + double(9)')
         ~is plusE(intE(1),
                   appE(idE(#'double), intE(9)))
  check: parse('3 * (4 + 8)')
         ~is multE(intE(3),
                   plusE(intE(4), intE(8)))
  check: parse('let x = 1 + 2:
                  y')
         ~is letE(#'x,
                  plusE(intE(1), intE(2)),
                  idE(#'y))
  check: parse('1 2')
         ~raises "invalid input"
         
  check: run('2')
         ~is 2
  check: run('x')
         ~raises "free variable"
  check: run('let x = 9:
                x')
         ~is 9
  check: run('2 + 1')
         ~is 3
  check: run('2 * 1')
         ~is 2
  check: run('(2 * 3) + (5 + 8)')
         ~is 19
  check: res.v(interp(parse('fun (x): x + x'), [], []))
         ~is closV(#'x, plusE(idE(#'x), idE(#'x)), mt_env)
  check: run('fun (x): x + x')
         ~raises "run cannot be used with closV"
  check: run('let x = 5: x + x')
         ~is 10
  check: run('let x = 5:
                         let x = x + 1:
                           x + x')

         ~is 12
  check: run('let x = 5:
                         let y = 6:
                           x')
         ~is 5
  check: run('(fun (x): x + x)(8)')
         ~is 16
  check: run('1(2)')
         ~raises "not a function"
  check: run('1 + (fun (x): x)')
         ~raises "not a number"
  check: run('let bad = (fun (x): x + y):
                         let y = 5:
                           bad(2)')
         ~raises "free variable"

module test:
  check: lookup(#'x, mt_env)
         ~raises "free variable"
  check: lookup(#'x, extend_env(bind(#'x, intV(8)), mt_env))
         ~is intV(8)
  check: lookup(#'x, extend_env(bind(#'x, intV(9)),
                                extend_env(bind(#'x, intV(8)),
                                           mt_env)))
         ~is intV(9)
  check: lookup(#'y, extend_env(bind(#'x, intV(9)),
                                extend_env(bind(#'y, intV(8)),
                                           mt_env)))
         ~is intV(8)